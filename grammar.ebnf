vowel = "a" | "u" | "o" | "i" | "e";

consonant = "p" | "b" | "f" | "v" | "m" | "w"
    | "t" | "d" | "s" | "z" | "r" | "c" | "l"
    | "k" | "g" | "h" | "y";

alphabet = vowel | consonant;

stem = alphabet+ ;

(*
nouns in Kratoma are normal nouns, called sokan.
*)

noun = stem
    | verb, "a"                   # the abstracted action of a verb
    | verb, "in"                  # the excecutor of a verb, person
    | verb, "wa"                  # the excecutor of a verb, not person
    | (verb | stative verb), "as" # the absolutive of a verb or stative verb
    | stative verb, "yu"          # the abstracted property of a stative verb
    | adjective stem, "van"       # the abstracted property of an adjective
    | adjective stem, "vei"       # some instance of an object satisfying the adjective
    | numeral, "k";               # the noun form of a numeral 

(*
verbs in Kratoma are divided into two classes, one of which is
verbs that can tell before and after, called burab.
*)

verb = stem 
    | verb, "em"              # to undo
    | verb, "u", verb         # two verbs combined
    | stative verb, "e", verb # stative verb combined with verb
    | verb, "ins"             # to be done
    | verb, "iks"             # to let do

(*
the other class of verb is stative verbs that cannot tell before
or after, called mosrab.
for the stative verb "muk" (to be), the negative form is "on"
instead of "mukon"
*)

stative verb = stem 
    | verb, "en"                          # negative form of a verb
    | verb, "eh"                          # “before” form of a verb
    | verb, "u", stative verb             # verb combined with stative verb
    | stative verb, "e", stative verb     # two stative verbs combined
    | stative verb - ("muk" | "on"), "on" # negative form of a stative verb
    | verb, "od";                         # indecates the purpose, “for doing”

(* 
adjectives, barely have any boundaries from stative verbs
called garasu
*)

adjective stem = stem
    | noun, "nu"            # form an adjective from a noun as "like ..."
    | noun, "lu"            # form an adjective from a noun as "about ..."
    | adjective stem, "kse" # more
    | adjective stem, "gru" # most
    | adjective stem, "na"  # negative form of an adjective
    | stative verb, "um"    # form an adjective from a stative verb
    | numeral, "ce";        # form an adjective from a numeral

adjective = adjective stem, "va"; 

(*
numerals are numbers and counting related words like "some",
"all", "first", "last", called unekin.
*)

numeral = stem 
    | numeral+; # multiple numerals form new numerals in some special rule

(*
as an agglutinative language, Kratoma relies on case particals
glued to nouns to form sentences.
*)

case partical = "zu" # the topic indecator
    | "su"  # the subjective indecator
    | "sem" # the tool indecator
    | "ko"  # the direction indecator
    | "bu"  # the objective indecator
    | "do"  # the dative indecator
    | "cu"  # the position indecator
    | "kaf" # the comitative indecator
    | "nof" # the anti-comitative indecator
    | "ye"  # the ablative / ergative (in passive form of verbs) indecator
    | "we"; # the terminative indecator (as "until" in English and "made" in Japanese)

single modifier = noun phrase, "ma"
    | verb clause, "o"
    | case partical, verb, "o"
    | stative verb clause, "i"
    | adjective phrase
    | numeral, "n";

modifier = single modifier, SPACE, modifier | single modifier;

single noun phrase = ( [ modifier, SPACE ] | "p", SPACE, modifier, SPACE, "s", SPACE), noun;

noun phrase = single noun phrase, ( "fu" | "zo" ), noun phrase | single noun phrase;

adverbial partical = "l" # like "mo" in Japanese; and reads as "t" after an "s"
    | "sy"   # like "sae" in Japanese and "even; as long as (in ...sy ...vi)" in English
    | "d"    # like "...uchi-ni" in Japanese and "while ..." in English
    | "v"    # like "dake" or "nomi" in Japanese and "only" in English
    | "ck";  # like "as (... as ...)" in English

basic adverbial = ( 
      noun phrase, case partical
    | verb clause, "u"
    | stative verb clause, "e"
    | adjective phrase stem, "vu"
    ), [ adverbial partical ] ;

(*
note that zu( l | sy | v ) is often replaced by ( l | sy | v )
*)

complex type = noun phrase, "kwa" # this is used in special adverbial clauses
    | verb clause, "i"
    | stative verb clause, "a"
    | adjective phrase stem, "ve";

complex adverbial indecator = "vi" # "if"
    | "vo"; # "as" or "since" or "given"

quotation = ANYTHING, SPACE, "t" | "p", SPACE, ANYTHING, SPACE, "sat";

single adverbial = basic adverbial | complex type, complex adverbial indecator;

adverbial = basic adverbial, SPACE, adverbial | basic adverbial;

final adverbial = adverbial | "p", SPACE, adverbial, SPACE, "s"

noun clause = [ noun, ( "su" | "zu" ), SPACE, ]
    [ final adverbial - (ANYTHING, noun, ( "su" | "zu" ), ANYTHING), SPACE] noun phrase;

verb clause = [ final adverbial, SPACE, ] [ quotation, SPACE, ] verb;

stative verb clause = [ final adverbial, SPACE, ] [ quotation, SPACE, ] stative verb;

adjective phrase stem = [ final adverbial, SPACE, ] adjective stem;

adjective phrase = adjective phrase stem, "va";

clause = noun clause
    | verb clause
    | stative verb clause
    | adjective phrase stem, "z";

dependent clause = verb clause, "uc"
    | stative verb clause, "ec"
    | adjective phrase stem, "vuc"
    | noun clause, "g"
    | complex adverbial

sentence = [ noun phrase, "yoo, ", ]
    { dependent clause, ", ", }
    ( clause | verb clause, "ai" | verb clause, "an" );
